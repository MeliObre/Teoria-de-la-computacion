package jcup;
import java.util.ArrayList;
import java.util.List;
import java_cup.runtime.*;

parser code
{:
    /**Metodo al que se llama automaticamente ante algun error sintactico.*/
    public void syntax_Error(Symbol s){
        System.out.println("Error en la linea " + (s.right+1) + " columna" + s.left + ". "
            + s + " no reconocido. valor " + s.value );
    }
:}

action code
{:
:}

/*Declaracion de Terminales */
terminal ID, PAR_ABR,PAR_CIE,COR_ABR,COR_CIE,OP_SUM,OP_RES,OP_DIV,OP_MUL,OP_MAY,OP_MEN,OP_DIST,OP_IGU,OP_MAYIG,OP_MENIG,
ASIG_VAR,ASIG_TIPO,CTE_ENT,CTE_REA,CTE_BIN,CTE_STR,BEGIN,END,WHILE,DO,IF,THEN,ELSE,ENDIF,WRITE,PERCENT,AND,
OR,SEC_DEC,SEC_END,SEC_PROG,SEC_ENDPROG,ASIG_FL,ASIG_INT,ASIG_STR,COMA;

/*Declaracion de No Terminales*/
non terminal prog,inicio, prt, sent_write, declaracion, programa, lista, asignacion_tipos, tipo, sentencia, iteracion,
condicional, salida, asignacion, condicion, comparacion, comparador, expresion, termino, factor, percent, cte, ident;

start with inicio;

inicio ::= prog {:System.out.println("[Regla 0] --> Compila correctamente llegando al símbolo start"); :};

prog ::= SEC_DEC declaracion SEC_END SEC_PROG programa SEC_ENDPROG {:System.out.println("[Regla 1] --> Programa completo"); :}
    | prt  {:System.out.println("[Regla 2] --> Programa sin declaraciones"); :};

prt ::= prt sent_write  {:System.out.println("[Regla 3] --> Linea de sentencia write"); :}
| sent_write  {:System.out.println("[Regla 4] --> Ultima sentencia write"); :};

sent_write ::= WRITE cte  {:System.out.println("[Regla 5] --> Write constante");:}
| WRITE ident  {:System.out.println("[Regla 6] --> Write identificador"); :};
/*| WRITE CTE_REA  {:System.out.println("[Regla 6]"); :}
| WRITE CTE_BIN  {:System.out.println("[Regla 7]"); :};**/

declaracion ::= asignacion_tipos declaracion   {:System.out.println("[Regla 7] --> Declaraciones"); :}
| asignacion_tipos {:System.out.println("[Regla 8] --> Linea de declaración "); :};

asignacion_tipos ::= COR_ABR:ca lista COR_CIE:cc  {:System.out.println("[Regla 9] --> [ lista ]");  :};

lista ::= ID:id COMA lista COMA tipo  {: System.out.println("[Regla 10] --> id, lista ,tipo" + id); RESULT = id;  :}
|  ID:id COR_CIE:cc ASIG_TIPO:at COR_ABR:ca tipo  {:System.out.println("[Regla 11] --> id] := [tipo" + id); RESULT = id;:};

tipo ::= ASIG_FL {:System.out.println("[Regla 12] --> Float"); :}
| ASIG_STR {:System.out.println("[Regla 13] --> String");:}
|  ASIG_INT {:System.out.println("[Regla 14] --> Integer"); :};

programa ::= programa sentencia  {:System.out.println("[Regla 15] --> Bloque de sentencias"); :}
| sentencia  {:System.out.println("[Regla 16] --> Sentencia"); :};

sentencia ::= iteracion  {:System.out.println("[Regla 17] --> Iteración"); :}
| condicional  {:System.out.println("[Regla 18] --> Condicional"); :}
| salida  {:System.out.println("[Regla 19] --> Salida"); :}
| asignacion  {:System.out.println("[Regla 20] --> Asignación"); :};

salida ::= WRITE ident   {:System.out.println("[Regla 22] --> Write identificador"); :}
| WRITE cte   {:System.out.println("[Regla 23] --> Write constante"); :};

condicional ::= IF PAR_ABR condicion PAR_CIE THEN programa ELSE programa ENDIF {:System.out.println("[Regla 21] --> IF (condición) THEN programa ELSE programa ENDIF"); :}
| IF PAR_ABR condicion PAR_CIE THEN programa ENDIF {:System.out.println("[Regla 22] --> IF (condición) THEN programa ENDIF"); :};

iteracion ::= WHILE PAR_ABR condicion PAR_CIE DO BEGIN programa END  {:System.out.println("[Regla 23] --> WHILE (condición) DO BEGIN programa END"); :};

condicion ::= comparacion  {:System.out.println("[Regla 24] --> Comparación"); :}
| comparacion AND comparacion  {:System.out.println("[Regla 25] --> Comparación AND Comparación"); :}
| comparacion OR comparacion  {:System.out.println("[Regla 26] --> Comparación OR Comparación"); :};

comparacion ::= expresion comparador expresion  {:System.out.println("[Regla 27] --> Expresión operador expresión"); :};

comparador ::= OP_MAY   {:System.out.println("[Regla 28] --> Operador mayor"); :}
| OP_MEN  {:System.out.println("[Regla 29] --> Operador menor"); :}
| OP_DIST  {:System.out.println("[Regla 30] --> Operador distinto"); :}
| OP_IGU  {:System.out.println("[Regla 31] --> Operador igual"); :}
| OP_MAYIG  {:System.out.println("[Regla 32] --> Operador mayor igual"); :}
| OP_MENIG {:System.out.println("[Regla 33] --> Operador menor igual"); :};

expresion ::= expresion OP_SUM termino {:System.out.println("[Regla 34] --> E+T"); :}
| expresion OP_RES termino  {:System.out.println("[Regla 35] --> E-T"); :}
| termino {:System.out.println("[Regla 36] --> T"); :};

termino ::= termino OP_MUL factor {:System.out.println("[Regla 37] --> T*F"); :}
| termino OP_DIV factor {:System.out.println("[Regla 38] --> T/F"); :}
|  factor:f {:System.out.println("[Regla 39] --> F valor = " + f); :};

ident ::= ID:id {:System.out.println("[Regla 40] --> Identificador = " + id); :};

cte ::= CTE_ENT:ent {:System.out.println("[Regla 41] --> Constante entera = " + ent); :}
| CTE_REA:rea {:System.out.println("[Regla 42] --> Constante real = " + rea); :}
| CTE_BIN:bin {:System.out.println("[Regla 43] --> Constante binaria " + bin); :}
| CTE_STR:str {:System.out.println("[Regla 44] --> Constante string " + str); :};

factor ::= PAR_ABR expresion PAR_CIE {:System.out.println("[Regla 45] --> (expresión)"); :}
| ident {:System.out.println("[Regla 46] --> Identificador"); :}
| cte {:System.out.println("[Regla 47] --> Constante"); :}
| percent {:System.out.println("[Regla 48] --> Funcion PERCENT"); :};

percent ::= PERCENT PAR_ABR expresion COMA expresion PAR_CIE {:System.out.println("[Regla 49] --> PERCENT(expresión,expresión)"); :};

asignacion ::= ID ASIG_VAR expresion {:System.out.println("[Regla 50] --> Asignación id ::= expresión"); :};